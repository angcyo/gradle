/**
 * 执行adb命令
 * */

def schema = project.hasProperty("schema") ? project.ext.schema : 'default'

/**通过root工程, 获取child所有子工程对应的application id*/
static def getApplicationIdList(rootProject) {
    def appList = []
    def childProjects = rootProject.getChildProjects()
    childProjects.each {
        def application = "com.android.application"

        def projectObj = it.value
        def isApp = projectObj.plugins.hasPlugin(application)

        if (isApp) {
            def defaultConfig
            if (gradle.gradleVersion.charAt(0) >= '8') {
                defaultConfig = projectObj.android.defaultConfig
            } else {
                def applicationPlugin = projectObj.plugins.findPlugin(application)
                defaultConfig = applicationPlugin.extension.defaultConfig
            }
            appList += defaultConfig.applicationId
        }
    }
    return appList
}

task _pullAllSchemaFolder(type: Exec, group: 'angcyo', description: '获取所有app工程Schema文件夹下的所有文件') {
    def path = '../.pull'
    def file = file(path)
    workingDir(path)
    def output = new ByteArrayOutputStream()
    standardOutput = output
    ext.output = {
        return standardOutput.toString("UTF8")
    }

    doFirst {
        file.mkdirs()
        def appIdList = getApplicationIdList(rootProject)
        println "准备pull:$appIdList -> " + file.absolutePath

        //获取设备列表
        /*
          List of devices attached
          901KPPB2061936	device
          9888d3484749523837	device
          emulator-5570	offline
        * */
        def deviceOutput = new ByteArrayOutputStream()
        exec {
            ignoreExitValue = true
            commandLine 'adb', 'devices'
            standardOutput = deviceOutput
        }
        def devices = deviceOutput.toString("UTF8")
        def devicesList = []
        devices.lines().skip(1).forEach {
            def ds = it.split("\t")
            if (ds.length > 1 && ds[1] == "device") {
                //在线的设备
                devicesList += ds[0]
            }
        }

        if (devicesList.isEmpty()) {
            throw new IllegalStateException("adb devices 未找到设备!")
        }
        println "拉取的设备列表↓"
        println devicesList

        devicesList.forEach { device ->
            appIdList.forEach { appId ->
                try {
                    exec {
                        ignoreExitValue = true
                        //files/${schema}/
                        commandLine 'adb', '-s', "${device}", 'pull', "/sdcard/android/data/${appId}/", file.absolutePath
                    }
                } catch (Exception e) {
                    System.err.println("异常:${e.message}")
                    e.printStackTrace()
                }
            }
        }

        //def target = "/sdcard/android/data/${appIdList.first()}/files/${schema}/"
        //commandLine 'adb', 'pull', target, './'

        if (isMac()) {
            commandLine 'echo', 'ing...'
        } else {
            commandLine 'cmd', '/C', 'echo', 'ing...'
        }
    }
    doLast {
        println "执行结束,文件在->${file.absolutePath}"
        println ext.output()
    }
}

//https://docs.gradle.org/current/dsl/org.gradle.api.tasks.Exec.html

/**adb pull 拉取手机sd上[/sdcard/android/data/package_name/files/xxx/]的所有文件*/

/**通过project工程, 获取对应的application id*/
static def getApplicationId(project) {
    def appId = ''
    def application = "com.android.application"

    def isApp = project.plugins.hasPlugin(application)

    if (isApp) {
        def applicationPlugin = project.plugins.findPlugin(application)
        appId = applicationPlugin.extension.defaultConfig.applicationId
    }
    return appId
}
/**使用code 打开拉取后的文件夹*/
task _pullAllSchemaFolderAndOpen(type: Exec, dependsOn: '_pullAllSchemaFolder', group: 'angcyo', description: '获取所有app工程的Schema文件夹下的所有文件并打开文件夹') {
    def path = '../.pull'
    def file = file(path)
    workingDir(path)
    doFirst {
        println "使用VS Code打开->${file.absolutePath}"

        if (isMac()) {
            commandLine 'code', file.absolutePath
        } else {
            commandLine 'cmd', '/C', 'code', file.absolutePath
        }
    }
}

task _rmAllSchemaFolder(type: Exec, group: 'angcyo', description: '删除所有app工程的Schema文件夹下的所有文件') {
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString("UTF8")
    }

    doFirst {
        def appIdList = getApplicationIdList(rootProject)

        appIdList.forEach { appId ->
            try {
                exec {
                    println "准备rm:$appId"
                    ignoreExitValue = true
                    commandLine 'adb', 'shell', 'rm', '-rf', "/sdcard/android/data/${appId}/files/${schema}/"
                }
            } catch (Exception e) {
                System.err.println("异常:${e.message}")
                e.printStackTrace()
            }
        }

        //def target = "/sdcard/android/data/${appIdList.first()}/files/${schema}/"
        //commandLine 'adb', 'pull', target, './'

        if (isMac()) {
            commandLine 'echo', 'ing...'
        } else {
            commandLine 'cmd', '/C', 'echo', 'ing...'
        }
    }
    doLast {
        println "执行结束!"
        println ext.output()
    }
}

task _rmSchemaFolder(type: Exec, group: 'angcyo', description: '获取第一个app工程的Schema文件夹下的所有文件') {
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString("UTF8")
    }
    ignoreExitValue = true
    doFirst {
        def appId = getApplicationId(project)
        println "准备rm:$appId"
        commandLine 'adb', 'shell', 'rm', '-rf', "/sdcard/android/data/${appId}/files/${schema}/"
    }
    doLast {
        println "执行结束!"
        println ext.output()
    }
}

import org.apache.tools.ant.taskdefs.condition.Os

task _dumpResumedActivity(group: 'angcyo', description: '获取当前活跃的Activity') {
    doLast {
        exec {
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                //adb shell dumpsys activity | findstr "mResumed")
                //commandLine("cmd", "/c", 'adb', 'shell', 'dumpsys', 'activity', '|', 'findstr', '"mResumed"')
                commandLine("cmd", "/c", 'adb shell dumpsys activity | findstr "mResumed"')
            } else {
                //adb shell dumpsys activity | grep "mResumed"
                commandLine('adb shell dumpsys activity | grep "mResumed"')
            }
        }
    }
}
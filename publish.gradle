/**
 * 1: apply from: "$gradleHost/master/publish.gradle"
 * 2: libGroupId = 'com.angcyo'
 * 3: libVersionName = '0.0.1'
 * 4: libWinRepo = 'E:/maven'
 * 5: libMacRepo = '/Users/angcyo'
 * 6: gradlew publish
 * */
//https://docs.gradle.org/current/userguide/publishing_maven.html
//默认输出路径<user>\.m2\repository
apply plugin: 'maven-publish'

/// 构建源码jar包
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}
task javadoc(type: Javadoc) {
    //增加编码，这行很重要，如果是windows系统，同时如果在代码中有中文的话，不加这行，上传会报错
    options.encoding = "utf-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

def libGroupId = project.hasProperty('libGroupId') ? project.property('libGroupId') : 'com.angcyo'
def versionName = project.hasProperty('libVersionName') ? project.property('libVersionName') : 'master-SNAPSHOT'//'0.0.1'
def winRepo = project.hasProperty('libWinRepo') ? project.property('libWinRepo') : 'E:/maven'
def macRepo = project.hasProperty('libMacRepo') ? project.property('libMacRepo') : '/Users/angcyo/maven'

//执行任务↓
//Executing tasks: [publish] in project E:\AndroidProjects\angcyo\UICoreDemo
//Executing tasks: [publishAllPublicationsToMavenRepository] in project E:\AndroidProjects\angcyo\UICoreDemo
//Executing tasks: [publishReleasePublicationToMavenRepository] in project E:\AndroidProjects\angcyo\UICoreDemo

//不需要发布aar的模块
ext.noPublishModule = []
def rootPath = getRootDir()
def noPublishFile = new File(rootPath, '.nopublish')
if (noPublishFile.exists()) {
    noPublishModule = noPublishFile.readLines('UTF8')
}

if (noPublishModule.size() > 0) {
    println "不发布以下Module->↓\n" + noPublishModule
}

afterEvaluate {
    publishing {
        def ignore = false
        if (new File(project.projectDir, ".nopublish").exists()) {
            ignore = true
        } else {
            noPublishModule.each {
                ignore = ignore || project.name == it
            }
        }

        if (ignore) {
            System.err.println("忽略Maven构建->" + project.name)
        } else {
            //产物收集配置
            publications {
                // Creates a Maven publication called "release".

                release(MavenPublication) {
                    // Applies the component for the release build variant.
                    if (!components.isEmpty()) {
                        from components.release
                    }

                    // You can then customize attributes of the publication as shown below.
                    groupId = libGroupId
                    artifactId = project.name
                    version = versionName

                    //artifact javadocJar
                    //artifact sourcesJar //2023-8-12 Invalid publication 'release': multiple artifacts with the identical extension and classifier ('jar', 'sources').
                }
            }

            //目标仓库配置
            repositories {
                //mavenLocal()
                maven {
                    // change to point to your repo, e.g. http://my.org/repo
                    //url = 'E:/AndroidProjects/maven'//"$buildDir/repo"

                    def os = System.getProperty("os.name").toLowerCase()
                    def path = os.contains("window") ? winRepo : macRepo
                    url = path

                    System.err.println(project.name + "->本地Maven路径:${path}")
                }
            }
        }
    }
}

//bintray {
//    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
//    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
//
//    publications = ['release']
//    configurations = ['archives']
//
//    //https://github.com/bintray/gradle-bintray-plugin#buildgradle
//    pkg {
//        repo = "maven"
//        name = "library"   //发布到JCenter上的项目名字
//        desc = 'create by angcyo.'
//        //userOrg = "angcyo"
//        websiteUrl = 'https://github.com/angcyo'
//        vcsUrl = 'https://github.com/angcyo'
//        licenses = ["MIT"]
//        publicDownloadNumbers = true
//        publish = true
//
//        /*version {
//            name = versionName
//            desc = 'library ' + versionName
//        }*/
//    }
//}
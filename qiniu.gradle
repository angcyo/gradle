/**
 * 七牛云 文件存储 上传脚本
 * 请无视IDEA的爆红, 并不影响运行.
 * 请不要随意导入其他包.
 *
 * 脚本`apply`之后, 会在gradle窗口中的: 工程->Tasks->other 中出现 `_qiniuUpload` 任务. 双击即可运行.
 *
 * https://github.com/angcyo/gradle
 *
 * https://developer.qiniu.com/kodo/sdk/1239/java
 *
 * <p>
 *
 * 其中关于Region对象和机房的关系如下：
 * https://developer.qiniu.com/kodo/1239/java#upload-config
 *
 * 机房	Region
 * 华东	Region.region0(), Region.huadong()
 * 华北	Region.region1(), Region.huabei()
 * 华南	Region.region2(), Region.huanan()
 * 北美	Region.regionNa0(), Region.beimei()
 * 东南亚	Region.regionAs0(), Region.xinjiapo()
 *
 * Email:angcyo@126.com
 * @author angcyo* @date 2019/08/09
 * Copyright (c) 2019 ShenZhen O&M Cloud Co., Ltd. All rights reserved.
 */

/**
 * 为了保证`Key`的安全:
 * 可以在
 * win: 'C:\Users\用户名\.gradle\gradle.properties"'
 * mac: '/Users/用户名/.gradle/gradle.properties"'
 * 文件中加入配置:
 * ```
 * qiniu_ak=xxx
 * qiniu_sk=xxx
 * qiniu_bucket=xxx
 * ```
 * 也可以在以下配置.(优先读取`gradle.properties`文件中的配置)
 * */
ext.qiniuConfig = [
        //需要上传的APK路径(必须指定)
        "qiniuApkPath"     : "",

        //是否要先执行删除文件
        "qiniuDeleteBefore": "true",

        //下载域名前缀, 需要'/'结尾(在七牛云上配置的外链域名)
        "qiniuBaseUrl"     : "",

        "qiniu_ak"         : "",
        "qiniu_sk"         : "",
        "qiniu_bucket"     : "",

        //如果需要将url转成二维码, 参考以下链接, 注册app_id和app_secret
        //https://github.com/MZCretin/RollToolsApi#%E8%A7%A3%E9%94%81%E6%96%B0%E6%96%B9%E5%BC%8F
        "app_id"           : "rhqkxdsnjnrpp3xr",
        "app_secret"       : "Rlh4c1duMDZ4YnZrRks4dU55c1NEQT09",

        //用来保存上传成功后, APK的下载地址
        "downloadUrl"      : "",
]

buildscript {
    repositories {
        //alliyun 镜像
        maven() { url 'https://maven.aliyun.com/repository/public' }
        mavenCentral()
    }
    dependencies {
        //https://github.com/qiniu/java-sdk/releases
        //https://jcenter.bintray.com/com/qiniu/qiniu-java-sdk/
        //https://search.maven.org/search?q=g:com.qiniu%20AND%20a:qiniu-java-sdk
        classpath 'com.qiniu:qiniu-java-sdk:7.8.0'
        classpath 'com.squareup.okhttp3:okhttp:3.14.4'
        //https://jcenter.bintray.com/com/google/code/gson/gson/
        classpath "com.google.code.gson:gson:2.8.6"
    }
}

/**
 * 上传本地文件->七牛云->返回文件下载地址
 * 拿到下载url->生成url二维码地址
 * */

task _qiniuUpload() {
    doFirst {
        def ak = project.hasProperty("qiniu_ak") ? project.ext.qiniu_ak : qiniuConfig.qiniu_ak
        def sk = project.hasProperty("qiniu_sk") ? project.ext.qiniu_sk : qiniuConfig.qiniu_sk
        def bk = project.hasProperty("qiniu_bucket") ? project.ext.qiniu_bucket : qiniuConfig.qiniu_bucket

        def baseUrl = project.hasProperty("qiniuBaseUrl") ? project.ext.qiniuBaseUrl : qiniuConfig.qiniuBaseUrl
        def qiniuDeleteBefore = project.hasProperty("qiniuDeleteBefore") ? project.ext.qiniuDeleteBefore : aliyunOssConfig.qiniuDeleteBefore
        def deleteBefore = qiniuDeleteBefore == "true"

        def app_id = project.hasProperty("app_id") ? project.ext.app_id : qiniuConfig.app_id
        def app_secret = project.hasProperty("app_id") ? project.ext.app_secret : qiniuConfig.app_secret
        def codeLogo = project.hasProperty("codeLogo") ? project.ext.codeLogo : null

        def apkPath = project.hasProperty("qiniuApkPath") ? project.ext.qiniuApkPath : qiniuConfig.qiniuApkPath
        if (apkPath == null || apkPath.isEmpty()) {
            throw new IllegalStateException("请在[gradle.properties]文件中指定需要上传的文件路径[qiniuApkPath=xxx]")
        }

        def allPath = apkPath.split(";")

        //创建上传凭证, 这是必须的.
        def auth = com.qiniu.util.Auth.create(ak, sk)

        allPath.each { path ->
            def apkFile = file(path)

            if (!apkFile.exists()) {
                if (allPath.size() > 1) {
                    System.err.println "跳过不存在的文件:" + apkFile.getAbsolutePath()
                } else {
                    throw new IllegalStateException("需要上传的文件不存在:" + apkFile.getAbsolutePath())
                }
            } else {
                def key = apkFile.name

                def putPolicy = new com.qiniu.util.StringMap()
                putPolicy.put("returnBody", "{\"key\":\"" + key + "\", \"hash\":\"" + System.currentTimeMillis() + "\"}")
                def expireSeconds = 3600L
                String upToken = auth.uploadToken(bk, null, expireSeconds, putPolicy)

                println "七牛云上传凭证:" + upToken

                //UploadManager uploadManager = new UploadManager()
                //Auth auth = Auth.create(accessKey, secretKey)
                //String token = auth.uploadToken(bucketName)
                //Response r = upManager.put("hello world".getBytes(), "yourkey", token)

                def downloadUrl = baseUrl + key
                System.err.println "\n开始上传->" + apkFile.getAbsolutePath() + "->" + downloadUrl

                //上传配置参考 https://developer.qiniu.com/kodo/sdk/1239/java#upload-config

                //华南地区
                //def cfg = new com.qiniu.storage.Configuration(com.qiniu.storage.Region.huanan())

                //自动判断
                def cfg = new com.qiniu.storage.Configuration(com.qiniu.storage.Region.autoRegion())

                if (deleteBefore) {
                    //删除文件
                    try {
                        def bucketManager = new com.qiniu.storage.BucketManager(auth, cfg)
                        println "删除文件:${bucketManager.delete(bk, key)}"
                    } catch (Exception ignored) {
                        //忽略异常
                    }
                }

                //上传本地文件
                def uploadManager = new com.qiniu.storage.UploadManager(cfg)
                def response = uploadManager.put(apkFile, key, upToken)
                def putRet = new com.google.gson.Gson().fromJson(response.bodyString(),
                        com.qiniu.storage.model.DefaultPutRet.class)

                println "上传完成->hash:${putRet.hash} key:${putRet.key}"

                qiniuConfig.downloadUrl = downloadUrl
                System.err.println "七牛云上传成功->" + qiniuConfig.downloadUrl

                if (!app_id.isEmpty() && !app_secret.isEmpty()) {

                    def url = ""
                    def request = null
                    if (codeLogo == null || codeLogo.isEmpty()) {
                        //无logo
                        url = "http://www.mxnzp.com/api/qrcode/create/single?content=${downloadUrl}&size=400&type=0"

                        request = new okhttp3.Request.Builder()
                                .header("app_id", app_id)
                                .header("app_secret", app_secret)
                                .url(url)
                                .build()
                    } else {
                        //有logo
                        url = "https://www.mxnzp.com/api/qrcode/create/logo?content=${downloadUrl}&size=400&type=0"

                        okhttp3.RequestBody requestBody = new okhttp3.MultipartBody.Builder()
                                .setType(okhttp3.MultipartBody.FORM)
                                .addFormDataPart("logo_img",
                                        "codeLogo",
                                        okhttp3.RequestBody.create(okhttp3.MediaType.parse("multipart/form-data"), new File(codeLogo)))
                                .build()

                        request = new okhttp3.Request.Builder()
                                .post(requestBody)
                                .header("app_id", app_id)
                                .header("app_secret", app_secret)
                                .url(url)
                                .build()
                    }

                    //生成url对应的二维码
                    //https://github.com/MZCretin/RollToolsApi#%E5%85%AB%E7%94%9F%E6%88%90%E4%BA%8C%E7%BB%B4%E7%A0%81
                    def client = new okhttp3.OkHttpClient()
                    def json = client.newCall(request).execute().body().string()
                    def map = new com.google.gson.Gson().fromJson(json, Map.class)
                    println "创建二维码:${json}"
                    System.err.println "二维码地址->" + map["data"]["qrCodeUrl"]

                    if (codeLogo == null || codeLogo.isEmpty()) {
                        System.err.println "备用二维码地址->http://qr.topscan.com/api.php?text=${downloadUrl}"
                    } else {
                        System.err.println "备用二维码地址->http://qr.topscan.com/api.php?text=${downloadUrl}&logo=${codeLogo}"
                    }
                }
            }
        }
    }
}
